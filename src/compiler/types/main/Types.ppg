(**
 * type structures.
 * @copyright (c) 2006, Tohoku University.
 * @author Atsushi Ohori
 * @author Liu Bochao
 * @author YAMATODANI Kiyoshi
 * @version $Id: Types.ppg,v 1.32 2007/05/15 06:14:02 kiyoshiy Exp $
 *)

(*
  2006/11/3 (Ohori) Changed the representatuon of strEnv.
  OLD:
    and strBindInfo =  STRUCTURE of strPathInfo
      ...
    withtyp 
     ...
    and strPathInfo = {id : id, name : string, strpath : path, env : Env}

    and strEnv =  strBindInfo SEnv.map
    and Env = tyConEnv * varEnv * strEnv

   This is cryptic and induces unnecessary dependency between withtype
   entries and datatype elements

  NEW:

   datatype strEnv = 
      STRUCTURE of ({id : id, name : string, strpath : path, env : Env} SEnv.map)
   withtype Env =   tyConEnv * varEnv * strEnv

   type strPathInfo =  {id : id, name : string, strpath : path, env : Env}

  This is clearer. strEnv is recursive in its env component.
  Note: "withtype Env" should be inlined and replace type Env. 
  Withtype is used here only to factor out the formatting comments.
*)

structure Types : TYPES =
struct

  (***************************************************************************)

  structure FE = SMLFormat.FormatExpression
  structure BF = SMLFormat.BasicFormatters

  (***************************************************************************)

  (*% 
   * @formatter(ID.id) ID.format_id
   *)
  type id = 
     (*%
      * @format(id) id
      *)
      ID.id

  (*% *)
  type tid = int
  val tidCompare = Int.compare
  val tidToString = Int.toString
  val initialTid = 0
  fun tidToInt tid = tid
  (* NOTE: this function is used only by pickler. *)
  fun intToTid int = int

(* this is a temporal definition *)
  (*% *)
  type lambdaDepth = int
  val infiniteDepth = 0x3fffffff (* the largest wokd in SML/NJ *)
  val toplevelDepth = 0
  fun youngerDepth {contextDepth, tyvarDepth} = contextDepth <= tyvarDepth
  fun strictlyYoungerDepth (tyvarDepth, contextDepth) = contextDepth < tyvarDepth
   
  (*% *)
  datatype eqKind =
           (*% @format "''" *)
           EQ 
         | (*% @format "'" *)
           NONEQ

 (*%
  *)
 datatype caseKind = 
     (*%
      * @format "bindCase"
      *)
     BIND 
   | (*%
      * @format "matchCase"
      *)
     MATCH
   | (*%
      * @format "handleCase"
      *)
     HANDLE

  (**
   * The canonical string representation for a bound type variable.
   *)
  fun tyIdName tid = 
      let
        fun numeral n = 
            if n < 26
            then [ord #"a" + n]
            else 
              let val (msb, rest) = (n mod 26, n div 26 - 1)
              in (ord #"a" + msb) :: (numeral  rest)
              end
      in (implode(map chr (rev (numeral tid))))
      end

  (**
   * The canonical string representation for a bound free type variable.
   *)
  fun freeTyIdName tid = 
      let
        fun numeral n = 
            if n < 26
            then [ord #"A" + n]
            else 
              let val (msb, rest) = (n mod 26, n div 26 - 1)
              in (ord #"A" + msb) :: (numeral  rest)
              end
      in (implode(map chr (rev (numeral tid))))
      end

  (**
   * * The canonical string representation for a dummy type variable.
   *)
  fun dummyTyIdName tid = "X" ^ Int.toString tid

  fun tyId2Doc (index,eqKind) = 
      let val prefix = case eqKind of EQ => "''" | NONEQ => "'"
      in (prefix ^ tyIdName index)
      end

  fun freeTyIdToDoc ({id, recKind, eqKind}) = 
      let val prefix = case eqKind of EQ => "''" | NONEQ => "'"
      in (prefix ^ (freeTyIdName id))
      end

  fun format_freeTyId values = BF.format_string(freeTyIdName values)
  fun format_dummyTyId values = BF.format_string(dummyTyIdName values)

  local 
    exception FreeBoundTy
  in
    fun formatBoundtvar (formatter, btvEnvs) id =
        let 
          fun findInfo nil = raise FreeBoundTy
            (* 
             (1000,NONEQ)
                    raise Control.Bug "TyIdToDoc, find index"
             *)
            | findInfo ((n, bEnv) :: rest) =
              (case IEnv.find(bEnv, id) of
                 SOME (tvKind as {index, eqKind, recKind}) =>
                 (n + index, eqKind)
               | NONE => findInfo rest)
          val (index, eqKind) = findInfo btvEnvs 
        in
          BF.format_string (tyId2Doc (index, eqKind))
        end
          handle FreeBoundTy => 
                 BF.format_string ("boundTvar(" ^ Int.toString id ^ ")")
  end

  (** formatter for 'a IEnv.map *)
  fun format_bmap_int (elementFormatter, prefix, suffix) values =
      let val separator = List.concat[[FE.Term(1, ",")]]
      in
        SmlppgUtil.formatListWithEnclosureOne
            (elementFormatter, separator, prefix, suffix)
            (IEnv.listItems values)
      end

  (** add bound type variable *)
  fun createBtvKindMap nil vars = [(0, vars)]
    | createBtvKindMap ((n, bEnv) :: rest) vars =
      (n + IEnv.numItems bEnv, vars) :: (n, bEnv) :: rest

  (** translate bound type variable to alphabet *)
  fun format_tyId values =
      let
        fun tyIdName tid = 
            let
              fun numeral n = 
                  if n < 26
                  then [ord #"a" + n]
                  else 
                    let val (msb, rest) = (n mod 26, (n div 26) - 1)
                    in (ord #"a" + msb) :: (numeral  rest)
                    end
            in
              (implode(map chr (rev (numeral tid))))
            end
      in
        BF.format_string(tyIdName values)
      end

  fun format_btvKind_index nil index = format_tyId index
    | format_btvKind_index ((n, bEnv) :: t) index = format_tyId (n + index)

  datatype path = datatype Path.path

  (*%
   * @formatter(Absyn.callingConvention) Absyn.format_callingConvention
   *)
  type callingConvention = Absyn.callingConvention
        
  (*% *)
  datatype sizeTagExp =
      (*%
       * @format(int) "CONST" int
       *)
      ST_CONST of int
    | (* id is tyCon Id *) 
      (*%
       * @format(id) "VAR" id
       *)
      ST_VAR of id
    | (*%
       * @format(id) "BDVAR" id
       *)
      ST_BDVAR of int
    | (*%
       * @format({stfun, args:arg args}) "APP(fun=" stfun ",args=" args(arg)()
       *)
      ST_APP of {stfun: sizeTagExp, args: sizeTagExp list}
    | (*%
       * @format({args:arg args, body}) "FUN" + args(arg)() + "=>" + body
         *)
      ST_FUN of {args : int list, body : sizeTagExp}


  (*%                
   *
   * @params(btvEnv)
   *
   * @formatter(listWithEnclosureOne) SmlppgUtil.formatListWithEnclosureOne
   * @formatter(tyConArgTys) SmlppgUtil.formatTyConArgumentTypes
   * @formatter(genericSmapTy) SmlppgUtil.formatGenericSmapTy
   * @formatter(smap) SmlppgUtil.formatSmap
   * @formatter(optWithDefault) SmlppgUtil.formatOptWithDefault
   * @formatter(binaryChoice) SmlppgUtil.formatBinaryChoice
   * @formatter(formatBoundtvar) formatBoundtvar
   * @formatter(format_freeTyId) format_freeTyId
   * @formatter(format_dummyTyId) format_dummyTyId
   * @formatter(format_bmap_int) format_bmap_int
   * @formatter(createBtvKindMap) createBtvKindMap
   * @formatter(format_btvKind_index) format_btvKind_index
   * @formatter(format_pathdot) Path.format_pathWithDotend
   *)
  datatype recKind =
      (*% @format "" *)
      UNIV 
    | (*% 
       * @format(ty smap:genericSmapTy)  
               {"#" 2[smap(ty()(btvEnv))(":", "," +1, "," +1)]}
       *)
      REC of ty SEnv.map
    | (*% 
       * @format(ty tys:listWithEnclosureOne)  
               {"#" 2[tys(ty()(btvEnv))("," +, "{", "}")]}
       *)
      OVERLOADED of ty list
      
  and tvState =
      (*% 
       * @format(value) value()(btvEnv) 
       *)
      TVAR of tvKind                  
    | (*% 
       * @format(value) value()(btvEnv) 
       *)
      SUBSTITUTED of ty
      
  and ty =
      (*% 
       * @format "errorty"
       *)
      ERRORty
    | (*% 
       * @format(id:format_dummyTyId)  id
       *)
      DUMMYty of int                  
    | (*% 
       * @format(a b) b(a()(btvEnv))
       *)
      TYVARty of tvState ref                  
    | (*%
       * @format(value:formatBoundtvar)
       *     value(value:btvKindWithoutKindInfo)(btvEnv)
       *)
      BOUNDVARty of int
    | (*% 
       * @format(left lefts:listWithEnclosureOne * right) 
       * R1{
       *     {lefts(left()(btvEnv))("," +, + "{", "}")} +d "->"
       *     2[+1 {right()(btvEnv)} ]
       *   }
       *)
      FUNMty of ty list * ty
    | (*% 
       * @format(elm smap:genericSmapTy) 
        *        smap(elm)(":", + "," +1, + "*" +1)
       * @format:elm(ty) 
            ty()(btvEnv)
       *)
      RECORDty of ty SEnv.map                          
    | (*% 
       * @format({tyCon, args:arg args:tyConArgTys})
       *    {args(arg()(btvEnv))("," +, "(", ")" +)} tyCon()(btvEnv)
       *)
      CONty of {tyCon : tyCon, args : ty list}
    | (*% 
       * @format({boundtvars:a:btvKind b, body}) 
       * !N0{
       *      "["
       *         b:format_bmap_int(a()(b:createBtvKindMap()(btvEnv)))("","")
       *         "."
       *         2[1 body()(b:createBtvKindMap()(btvEnv))]
       *      "]"
       *    }
       *)
      (**
       * boundtvars =
       *      {..., btvid -> btvKind as {index, recKind, eqKind}, ...}
       * where idex is the location of the boundtvars used only for printing.
       * btvid is the id of boundTvars used as i in BOUNDVARty(i)
       *)
      POLYty of {boundtvars : btvKind IEnv.map, body : ty}
    | (*% 
       * @format "BOXEDty" 
       *)
      BOXEDty (* generic boxed type *)
    | (*% 
       * @format "ATOMty" 
       *)
      ATOMty (* generic unboxed type *)
    | (*% 
       * @format "GENERICty" 
       *)
      GENERICty (* generic type *)
    | (*% 
       * @format(ty * id) !R0{"index(" ty()(btvEnv) "," id ")"}
       *)
      INDEXty of ty * string
    | (*% 
       * @format(left lefts:listWithEnclosureOne * right) 
       * R1{
       *     {lefts(left()(btvEnv))("," +, + "{", "}")} +d "->"
       *     2[+1 {right()(btvEnv)} ]
       *   }
       *)
      BMABSty of ty list * ty
    | (*
       * @format(bit bits) !R0{"BITMAPty"}
       *)
      (*%
       * @format(bit bits) !R0{"|" 2[1 bits(bit()(btvEnv))("," 1) ] "|"}
       *)
      BITMAPty of ty list 
    | (*%
       * @format(ty1 * ty2) ty1()(btvEnv)
       *)
      ALIASty of ty * ty
    | (*% 
       * @format(value:formatBoundtvar)
       *        "|" value(value:btvKindWithoutKindInfo)(btvEnv) "|"
       *)
      BITty of int
    | (*% 
       * @format "UNBOXEDty" 
       *)
      UNBOXEDty
    | (*% 
       * @format "DBLUNBOXEDty" 
       *)
      DBLUNBOXEDty
    | (*%
       * @format(ty tys) !R0{"OF[" 2[1 tys(ty()(btvEnv))("," 1) ] "]"}
       *)
      OFFSETty of ty list 
    | (*% 
       * @format(value:formatBoundtvar)
       *        "|" value(value:btvKindWithoutKindInfo)(btvEnv) "|"
       *)
      TAGty of int
    | (*% 
       * @format(value:formatBoundtvar)
       *        "<" value(value:btvKindWithoutKindInfo)(btvEnv) ">"
       *)
      SIZEty of int
    | (*% 
       * @format "DOUBLEty" 
       *)
      DOUBLEty
    | (*%
       * @format(ty tys) !R0{"PA[" 2[1 tys(ty()(btvEnv))("," 1) ] "]"}
       *)
      PADty of ty list
    | (*%
       * @format(ty tys * tid:formatBoundtvar) 
       *        !R0{"PC[" tid(tid:btvKindWithoutKindInfo)(btvEnv)
       *                  "=>"
       *                  2[1 tys(ty()(btvEnv))("," 1) ] 
       *              "]"}
       *)
      PADCONDty of ty list * int
    | (*% 
       * @format(v) "FRAMEBITMAPty" 
       *)
      FRAMEBITMAPty of int list
    | (*% 
       * @format(specty * realty)  specty()(btvEnv) 
       *)
      ABSSPECty of ty * ty 
    | (*% 
       * @format(ty)  ty()(btvEnv)
       *)
      SPECty of ty
    | (*% 
       * @format(abstractty)  "ABSTRACTty"
       *)
      (* only used under separate compilation *)
      ABSTRACTty 

  and idState =
      (*%
       * @format(var) var()(btvEnv)
       *)
      VARID of varPathInfo
    | (*%
       * @format({name, strpath, funtyCon, ty, tag, tyCon})
       * name + ":" + ty()(btvEnv) 
       *)
      CONID of
      (* This is the conPathInfo.
       * inlined here to break the dependency among withtype entries
       *)
      {
        name : string,
        strpath : path,
        funtyCon : bool,
        ty : ty,
        tag: int,
        tyCon : tyCon
      }
    | (*%
       * @format(prim) prim()(btvEnv)
       *)
      PRIM of primInfo
    | (*%
       * @format(oprim) oprim()(btvEnv)
       *)
      OPRIM of oprimInfo
    | (*%
       * @format(var * arity) var()(btvEnv)
       *)
      RECFUNID of varPathInfo * int

  withtype tvKind =
      (*
       Ohori: Dec 3, 2006.
       lambdaDepth is introduce to speed up type generalization.
       Initially, it is equal to the length of \Gamma at which a type
       variable is introduced. The top level is therefore 0.
       We then maintain the invariant:
         lambdaDepth(t) is the minimal |\Gamma|  such that
              \Gamma{x:\tau(t)}
       A type variable is created with lambdaDepth = INFINITE.
       When a type is entered in \Gamma{x:\tau}, the lambdaDepth of 
       each type variable t' in tau is set to |\Gamma|
       Whenever (t,tau) is unified, the lambdaDepth of each type variable t'
       in tau must be set to min(lambdaDepth(t), lambdaDepth(t')).
      *)
      (*% 
       * @format({lambdaDepth, id, recKind, eqKind, tyvarName:name opt:optWithDefault}) 
       *  eqKind opt(name)(id:format_freeTyId recKind()(btvEnv))
       *)
      {
       lambdaDepth : lambdaDepth,
       id : int,
       recKind : recKind, 
       eqKind : eqKind, 
       tyvarName : string option
       }

  and btvKind =
      (*% 
       * @format({index, recKind, eqKind})
       *  eqKind index:format_btvKind_index()(btvEnv) recKind()(btvEnv) 
       *)
      (**
        * index is the location within the btvEnv. use only for printing(?)
       *)
      {index : int, recKind : recKind, eqKind : eqKind}
      (**
       * this is a dummy type unsed only for printing 
       *)

  and btvKindWithoutKindInfo =
      (*% 
       * @format({index, recKind, eqKind})
       *     eqKind index:format_btvKind_index()(btvEnv)
       *)
      {index : int, recKind : recKind, eqKind : eqKind}

  and tyCon =
      (*% 
       * @format({name, 
                  strpath, 
		  abstract, 
		  tyvars, 
		  id, 
		  eqKind:eqKind refcon, 
		  boxedKind:boxedKind bref, 
                  datacon})
       * strpath:format_pathdot name 
       *)
      (* NOTE: strpath is absolute path. *)
      (* Note for print id and boxedKind : ",id =" id ",boxedkind=" bref(boxedKind()(btvEnv)) *)
      {
        name : string,
        strpath : path,
        tyvars : bool list,
        id : id,
	abstract : bool,
        eqKind : eqKind ref,
	boxedKind : ty ref,
        (* 
         * idState SEnv.map = varEnv
         * inlined below to break cyclic dependency 
         * between withtype entry and datatype entry 
         *)
        datacon : (idState SEnv.map) ref 
      }

  and varPathInfo =
      (*%
       * @format({name,strpath:path:format_pathdot,ty})
       *        path name + ":" + ty()(btvEnv)
       *)
      {name :string, strpath : path, ty : ty}

  and primInfo = 
      (*%
       * @format({name, ty}) name + ":" + ty()(btvEnv)
       *)
      {name : string, ty : ty}

  and oprimInfo = 
      (*%
       * @format({name, ty, instances}) name + ":" + ty()(btvEnv)
       *)
      {
       name : string, 
       ty : ty, 
       (* 
        * instances : primInfo SEnv.map
        * the instances are set of primInfos
	* inlined here to break dependency among withtype entry
	*)
       instances : {name : string, ty : ty} SEnv.map
       }


  (*%                
   * @params(btvEnv)
   *)
  type conPathInfo =
      (*%
       * @format({name, strpath, funtyCon, ty, tag, tyCon})
       * name + ":" + ty()(btvEnv) 
       *)
      {
        name : string,
        strpath : path,
        funtyCon : bool,
        ty : ty,
        tag: int,
        tyCon : tyCon
      }

  (*% 
   * @formatter(format_pathdot) Path.format_pathWithDotend
   *)
  type conPathInfoName = 
      (*%
       * @format({name, strpath, funtyCon, ty, tag, tyCon})
          strpath:format_pathdot name
       *)
      {
        name : string,
        strpath : path,
        funtyCon : bool,
        ty : ty,
        tag : int,
        tyCon : tyCon
      }

  (*%  
   * @params(btvEnv)
   *)
  type conPathInfoNameType =
      (*%
       * @format({name, strpath, funtyCon, ty, tag, tyCon})
       *        name + ":" + ty()(btvEnv)
       *)
      {
        name : string,
        strpath : path,
        funtyCon : bool,
        ty : ty,
        tag: int,
        tyCon : tyCon
      }


(*
 ***********************************************************************
 * for type environments
 ***********************************************************************
*)

  (*%                
   * @params(btvEnv)
   * @formatter(format_bmap_int) format_bmap_int
   * @formatter(createBtvKindMap) createBtvKindMap
   *)
  type tyFun =
      (*% 
       * @format({name, tyargs:a:btvKind b, body}) 
       *    b:format_bmap_int(a()(b:createBtvKindMap()(btvEnv)))("(",") ")
       *    name
       *    +
       *    "=" +d 2[1 body()(b:createBtvKindMap()(btvEnv))]
       *)
      {name : string, tyargs : btvKind IEnv.map, body : ty}

  (*%                
   * @params(btvEnv)
   * @formatter(format_pathdot) Path.format_pathWithDotend
   *)
  type tySpec =
      (*% 
       * @format({
       *           name,
       *           tyvars,
       *           id,
       *           strpath,
       *           eqKind,
       *           boxedKind
       *         })
       *      strpath:format_pathdot name + "=" +d boxedKind()(btvEnv)
       *)
      (* NOTE: strpath is absolute path. *)
      {name : string, id : id, strpath : path, eqKind : eqKind, 
       tyvars : bool list, boxedKind : ty}

  (*%                
   * @params(btvEnv)
   *)
  datatype tyBindInfo =
      (*% 
       * @format(tyCon) "tyCon{" + tyCon()(btvEnv) + "}"
       *)
      TYCON of tyCon
    | (*% 
       * @format(tyFun) "tyFun{" + tyFun()(btvEnv) + "}"
       *)
      TYFUN of tyFun
    | (*% 
       * @format({spec,impl: impl opt})
       * N10{ "{"
       *     "spec" + "=" + spec()(btvEnv)
       *     +1
       *     "impl" + "=" +d opt(impl()(btvEnv))
       * "}" }
       *)
      TYSPEC of {spec : tySpec, impl : tyBindInfo option} 

  
  (*%                
   * @params(btvEnv)
   * @formatter(smapWithEnclosure) SmlppgUtil.formatSmapWithEnclosure
   *)
  type tyConEnv = 
       (*%
        * @format(tyCon smap:smapWithEnclosure) 
            smap(tyCon()(btvEnv))("=", +1, "typebinds" + 1, "")	
        *)
       tyBindInfo SEnv.map

  (*%                
   * @params(btvEnv)
   * @formatter(smap) SmlppgUtil.formatSmap
   *)
  type utvEnv =
      (*%
       * @format(tvStateRef smap:smap) smap(tvStateRef)(":",+1) "\n"
       * @format:tvStateRef(tvState r) r(tvState()(btvEnv))
       *)
      (tvState ref) SEnv.map


(*
 ***********************************************************************
 * for value environments
 ***********************************************************************
*)
 
  (*%                
   * @params(btvEnv)
   * @formatter(smap) SmlppgUtil.formatSmap
   *)
  type varEnv = 
      (*%
       * @format(idState smap:smap) smap(idState()(btvEnv))(":",+1) "\n"
       *)
      idState SEnv.map

  (*%                
   * @params(btvEnv)
   * @formatter(smap) SmlppgUtil.formatSmap
   * @formatter(format_path) Path.format_pathWithoutDotend
   *)
   datatype strEnv = 
      (*%
       * @format(strEnv smap:smap) smap(strEnv)(":",1) "\n"
       * @format:strEnv({id, name, strpath, env}) strpath:format_path name +1 env()(btvEnv)
       *)
      STRUCTURE of ({id : id, name : string, strpath : path, env : Env} SEnv.map)

   withtype Env = 
      (*%
       * @format(tyConEnv * varEnv * strEnv)
       *   "TCENV:" tyConEnv()(btvEnv) +1
       *   "VARENV:" +1 varEnv()(btvEnv) +1
       *   "STRENV:" strEnv()(btvEnv)
       *)
       tyConEnv * varEnv * strEnv

  (*%                
   * @params(btvEnv)
   *)
  type strInfo =
       (*%
        * @format({id, name, env}) name
        *)
       {id : id, name : string, env : Env}

  (*%                
   * @params(btvEnv)
   * @formatter(format_path) Path.format_pathWithoutDotend
   *)
  type strPathInfo =
       (*%
        * @format({id, name, strpath, env}) strpath:format_path name +1  env()(btvEnv)
        *)
       {id : id, name : string, strpath : path, env : Env}

  (*%                
   * @params(btvEnv)
   *)
  type varIdInfo = 
      (*%
       * @format({id, displayName, ty}) { displayName + ":" +1 ty()(btvEnv) }
       *)
      {id : id, displayName : string, ty : ty}

  (*%                
   * @params(btvEnv)
   *)
  type varIdInfoWithoutType = 
      (*%
       * @format({id, displayName, ty}) displayName
       *)
      {id : id, displayName : string, ty : ty}

 (*%
   * @params(btvEnv)
  *)
  datatype valId = 
     (*%
      * @format({name,ty:ty}) name
      *)
     (*
      * @format({name,ty:ty}) name "[" ty()(btvEnv) "]"
      *)
     VALIDVAR of {name : string, ty : ty}
   | (*%
      * @format(ty) "_"
      *)
     VALIDWILD of ty

 (** used after modulecompilation **)
 (*%
   * @params(btvEnv)
  *)
 datatype valIdent = 
     (*%
      * @format(varIdInfo) varIdInfo()(btvEnv)
      *)
     VALIDENT of varIdInfo 
   | (*%
      * @format(ty) "_"
      *)
     VALIDENTWILD of ty


  (*%                
   * @params(btvEnv)
   *)
  type conInfo = 
      (*%
       * @format({displayName, funtyCon, ty, tag, tyCon})
       * displayName + ":" + ty()(btvEnv)
       *)
      {displayName : string, funtyCon : bool, ty : ty, tag: int, tyCon : tyCon}

  (*%                
   * @params(btvEnv)
   *)
  type conInfoNameType = 
      (*%
       * @format({displayName, funtyCon, ty, tag, tyCon}) displayName + ty()(btvEnv)
       *)
      {displayName : string, funtyCon : bool, ty : ty, tag: int, tyCon : tyCon}

  (*%  *)
  (** dummy type definition for formatter definiton *)
  type conInfoName = 
      (*%
       * @format({displayName, funtyCon, ty, tag, tyCon}) displayName
       *)
      {displayName : string, funtyCon : bool, ty : ty, tag : int, tyCon : tyCon}

  type subst = ty IEnv.map
  type btvEnv = btvKind IEnv.map

  val univKind = {recKind = UNIV, eqKind = NONEQ, tyvarName = NONE}
 
  local
    fun format_tvar (name, isEq) =
        SmlppgUtil.formatBinaryChoice
            (
              [FE.Term(2, "''"), FE.Term(size name, name)],
              [FE.Term(1, "'"), FE.Term(size name, name)]
            )
            isEq
    fun format_smap parm smap =
        SmlppgUtil.formatListWithEnclosure parm (SEnv.listItemsi smap)
  in
  (*%
   * @formatter(smap) format_smap
   * @formatter(tvar) format_tvar
   *)
  type tvarNameSet = 
       (*%
        * @format(b:tvar map:smap) map(b)(",","(",") ") 
        *)
       bool SEnv.map 
  end

  val format_IDSet =  SmlppgUtil.formatIDSet
                          ([FE.Term(1, ",")],
                           [FE.Term(1, "(")],
                           [FE.Term(1, ")")]) 
  (*%
   * @formatter(ID.Set.set) format_IDSet
   *)
  type tyConIdSet = 
       (*%
        * @format(set) set
        *)
       ID.Set.set

  (*%
   * @params(btvEnv)
   *)
  datatype sigBindInfo = 
           (*%
            * @format (tyConIdSet * strPathInfo) strPathInfo()(btvEnv)
            *)
           SIGNATURE of tyConIdSet * strPathInfo
  (*%
   * @params(btvEnv)
   * @formatter(smap) SmlppgUtil.formatSmap
   *)
  type sigEnv = 
       (*%
        * @format(sigma smap:smap) smap(sigma()(btvEnv))(":",+1) "\n"
        *)
       sigBindInfo SEnv.map 

  local
      fun formatExnTagSet exnSet = 
          let 
              val L = map (fn tag => Int.toString(tag))
                          (ISet.listItems exnSet)
          in
              SmlppgUtil.formatListWithEnclosureOne
                  (
                   SMLFormat.BasicFormatters.format_string,
               [FE.Term(1, ",")],
               [FE.Term(1, "(")],
               [FE.Term(1, ")")]
               )
                  L
          end
  in
  (*%
   * @formatter(formatExnTagSet) formatExnTagSet
   *)
  type exnTagSet = 
       (*%
        * @format(exnTagSet:formatExnTagSet) exnTagSet  
        *)
       ISet.set
  val  emptyExnTagSet = ISet.empty
  end
       
  (*%
   *)
  type funBindInfo =
       (*%
        * @format({func : {name,id}, argument, functorSig}) name
        *)
       { 
	func : {name:string, id : id},
	argument : {name:string,id :id},
	functorSig: {
		     exnTagSet : exnTagSet,
		     tyConIdSet : tyConIdSet, 
		     func : {
			     arg : Env, 
			     body : {
				     constrained:(tyConIdSet * Env),
				     unConstrained: Env}
				    }
			    }
       }
       
  (*%
   * @formatter(funBindInfo) format_funBindInfo
   * @formatter(smap) SmlppgUtil.formatSmap
   *)
  type funEnv = 
       (*%
        * @format(funBindInfo smap:smap) smap(funBindInfo:funBindInfo)(":",+1) "\n"
        *)
       funBindInfo SEnv.map

  (**************************************************************************)

  val emptyVarEnv = SEnv.empty : varEnv
  val emptyTyfield = SEnv.empty : ty SEnv.map
  val emptyTyConEnv = SEnv.empty : tyConEnv 
  val emptyStrEnv = STRUCTURE SEnv.empty :strEnv
  val emptySigEnv = SEnv.empty :sigEnv
  val emptyFunEnv = SEnv.empty :funEnv
  val emptyE = (emptyTyConEnv, emptyVarEnv, emptyStrEnv) : Env
  val emptySubst = IEnv.empty : subst

  val tidSequenceRef = ref (SequentialNumber.generateSequence initialTid)
  val btidSequenceRef = ref (SequentialNumber.generateSequence 0)
  fun initTid () = SequentialNumber.init (!tidSequenceRef)
  fun nextTid () = SequentialNumber.generate (!tidSequenceRef)
  fun peekTid () = SequentialNumber.peek (!tidSequenceRef)
  fun nextBTid () = SequentialNumber.generate (!btidSequenceRef)
  fun peekBTid () = SequentialNumber.peek (!btidSequenceRef)
  fun advanceBTid count = SequentialNumber.advance (!btidSequenceRef) count

  fun nextTyConId () = ID.peek ()
  fun newTyConId () = ID.generate ()

  fun nextVarId () = ID.peek ()
  fun newVarId () = ID.generate ()

  val dummyStructureId = ID.reserve ()
  fun nextStructureId () = ID.peek ()
  fun newStructureId () = ID.generate ()

  (* NOTE: exception tag is not global ID. 
   *)
  val maxSystemExnTag = 10
  (* NOTE: exnConIdSequenceRef is updated by Top.unpickle.
   *)
  val exnConIdSequenceRef =
      ref(SequentialNumber.generateSequence maxSystemExnTag)
  fun nextExnTag () = SequentialNumber.peek (!exnConIdSequenceRef)
  fun newExnTag() = SequentialNumber.generate (!exnConIdSequenceRef)

  fun conPathInfoToConInfo (conpath : conPathInfo) =
      let
        val {name, strpath, funtyCon, ty, tyCon, tag} = conpath
      in
        if Path.comparePathByName (Path.topStrPath, strpath)
        then
          {
            displayName = name,
            funtyCon = funtyCon,
            ty = ty,
            tyCon = tyCon,
            tag = tag
          } : conInfo
        else
          raise
            Control.Bug "conpathInfoToConInfo expects top leve exn constructor."
      end

  val kindedTyvarList = ref nil : tvState ref list ref

  (** true if two tyCons are the same. *)
  fun eqTyCon (left : tyCon, right : tyCon) = ID.eq(#id left, #id right)

  fun newTvStateRef {lambdaDepth, recKind, eqKind, tyvarName} =
    let 
      val newTv = (ref
                   (TVAR
                    {
		     lambdaDepth = lambdaDepth,
                     id = nextTid(),
                     recKind = recKind,
                     eqKind = eqKind, 
                     tyvarName = tyvarName
                     }))
      val _ = kindedTyvarList := newTv::(!kindedTyvarList)
    (*
       case recKind of
	     UNIV => ()
          | REC _ => kindedTyvarList := newTv::(!kindedTyvarList)
	  | OVERLOADED _ => kindedTyvarList := newTv::(!kindedTyvarList)
     *)
    in
      newTv
    end
  
  fun newty {recKind, eqKind, tyvarName} = 
      TYVARty (newTvStateRef 
               {
	        lambdaDepth=infiniteDepth, 
	        recKind=recKind, 
	        eqKind=eqKind, 
	        tyvarName=tyvarName
	       })

  fun newtyWithLambdaDepth (lambdaDepth, {recKind, eqKind, tyvarName}) = 
      TYVARty (newTvStateRef 
               {
	        lambdaDepth=lambdaDepth, 
	        recKind=recKind, 
	        eqKind=eqKind, 
	        tyvarName=tyvarName
	       })

  fun newtyRaw {lambdaDepth, recKind, eqKind, tyvarName} = 
      TYVARty (newTvStateRef 
               {
	        lambdaDepth=lambdaDepth, 
	        recKind=recKind, 
	        eqKind=eqKind, 
	        tyvarName=tyvarName
	       })

  fun newUtvar (lambdaDepth, eqKind, tvarName) =
      newTvStateRef 
       {
        lambdaDepth = lambdaDepth , 
        recKind=UNIV, 
        eqKind=eqKind, 
        tyvarName= SOME tvarName
        }

  type moduleState =
       (
         SequentialNumber.sequence *
         SequentialNumber.sequence *
         SequentialNumber.sequence
       )

  fun getModuleState () =
      (!tidSequenceRef, !btidSequenceRef, !exnConIdSequenceRef)

  fun restoreModuleState
          (newTidSequence, newBtidSequence, newExnConIdSequence) =
      (
        tidSequenceRef := newTidSequence;
        btidSequenceRef := newBtidSequence;
        exnConIdSequenceRef := newExnConIdSequence;
        ID.init () (* ToDo : Why Types initializes ID here ? *)
      )

  fun init () =
      (
        SequentialNumber.init (!tidSequenceRef);
        SequentialNumber.init (!btidSequenceRef);
        SequentialNumber.init (!exnConIdSequenceRef);
        ID.init () (* ToDo : Why Types initializes ID here ? *)
      )

end
