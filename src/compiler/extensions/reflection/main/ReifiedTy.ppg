(**
 * Type representation for types
 *
 * @copyright (c) 2016, Tohoku University.
 * @author Atsushi Ohori
 *)
structure ReifiedTy =
struct

  exception ConSetNotFound
  exception GetConSetFail
  exception InstantiateFail
  exception GetConSetFail

  (*%
     @formatter(BoundTypeVarID.id) BoundTypeVarID.format_id
   *)
  type btvId
    = (*% @format(id) "'" id *)
      BoundTypeVarID.id

  type typId = TypID.id

   (*%
    * @params(labelsep)
    * @formatter(list)  SMLFormat.BasicFormatters.format_list
    *)
   type  btvList =
     (*%
        @format(field fields)
          fields(field)(","+1) 
        @format:field(label * elem) label
      *)
     (btvId * btvId) list

  fun formatBtvenv (labelsep) env =
      format_btvList(labelsep) (BoundTypeVarID.Map.listItemsi env)

  (*%
     @formatter(SEnv.map) TermPrintUtils.formatSEnvMap
   *)
  datatype taggedLayout 
    = (*% 
         @format({tagMap:tag map})
          !N0{ "TAGGED_RECORD{tagMap:{" 
                2[1 map(tag)(":") ] "}" 1 "}" }
       *)
      TAGGED_RECORD of {tagMap: int SEnv.map}
    | (*% 
          @format({tagMap: tag map, nullName:name})
          !N0{ "TAGGED_OR_NULL{tagMap:{" 
                2[1 map(tag)(":") ] "}," + "nullName:" name 1 "}" }
       *)
      TAGGED_OR_NULL of {tagMap: int SEnv.map, nullName: string}
    | (*% 
          @format({tagMap:tag map})
          !N0{ "TAGGED_TAGONLY{tagMap:{" 2[1 map(tag)(":") ] "}" 1 "}" }
       *)
      TAGGED_TAGONLY of {tagMap: int SEnv.map}
  fun taggedLayoutToString reifiedTy =
      SMLFormat.prettyPrint nil (format_taggedLayout reifiedTy)
  fun taggedLayoutEq (TAGGED_RECORD {tagMap=map1}, TAGGED_RECORD {tagMap=map2}) =
      SEnv.isEmpty
        (SEnv.mergeWith 
           (fn (SOME a, SOME b) => if a = b then NONE else SOME (SOME a,SOME b)
             | (SOME a, NONE) => SOME (SOME a, NONE)
             | (NONE, SOME b) => SOME (NONE, SOME b)
             | (NONE, NONE) => NONE
           )
           (map1,map2)
        )
    | taggedLayoutEq (TAGGED_OR_NULL {tagMap=map1, nullName = name1},
                      TAGGED_OR_NULL {tagMap=map2, nullName = name2}) =
      name1 = name2 andalso
      SEnv.isEmpty
        (SEnv.mergeWith 
           (fn (SOME a, SOME b) => if a = b then NONE else SOME (SOME a,SOME b)
             | (SOME a, NONE) => SOME (SOME a, NONE)
             | (NONE, SOME b) => SOME (NONE, SOME b)
             | (NONE, NONE) => NONE
           )
           (map1, map2)
        )
    | taggedLayoutEq(TAGGED_TAGONLY {tagMap=map1},TAGGED_TAGONLY {tagMap=map2}) =
      SEnv.isEmpty
        (SEnv.mergeWith 
           (fn (SOME a, SOME b) => if a = b then NONE else SOME (SOME a,SOME b)
             | (SOME a, NONE) => SOME (SOME a, NONE)
             | (NONE, SOME b) => SOME (NONE, SOME b)
             | (NONE, NONE) => NONE
           )
           (map1, map2)
        )
    | taggedLayoutEq _ = false

  (*%
     @formatter(SEnv.map) TermPrintUtils.formatSEnvMap
   *)
  datatype layout
    = (*%
         @format(taggedLayout)
           "LAYOUT_TAGGED(" taggedLayout ")"
       *)
      LAYOUT_TAGGED of taggedLayout
    | (*% 
          @format({wrap})
         "LAYOUT_ARG_OR_NULL{wrap:" wrap "}"
       *)
      LAYOUT_ARG_OR_NULL of {wrap: bool}
    | (*% 
          @format({wrap})
           "LAYOUT_SINGLE_ARG{wrap:" wrap  "}"
       *)
      LAYOUT_SINGLE_ARG of {wrap: bool}
    | (*% 
          @format({falseName})
           "LAYOUT_CHOICE{falseName:" falseName  "}"
       *)
      LAYOUT_CHOICE of {falseName: string}
    | (*% 
          @format  "LAYOUT_SINGLE"
       *)
      LAYOUT_SINGLE
  fun layoutToString reifiedTy =
      SMLFormat.prettyPrint nil (format_layout reifiedTy)
  fun layoutEq (LAYOUT_TAGGED taggedLayout1, LAYOUT_TAGGED taggedLayout2) = 
      taggedLayoutEq (taggedLayout1, taggedLayout2)
    | layoutEq (LAYOUT_ARG_OR_NULL{wrap=wrap1}, LAYOUT_ARG_OR_NULL{wrap=wrap2}) =
      wrap1 = wrap2
    | layoutEq (LAYOUT_SINGLE_ARG {wrap =wrap1}, LAYOUT_SINGLE_ARG {wrap =wrap2}) =
      wrap1 = wrap2
    | layoutEq (LAYOUT_CHOICE {falseName=name1}, LAYOUT_CHOICE {falseName=name2}) =
      name1 = name2
    | layoutEq (LAYOUT_SINGLE, LAYOUT_SINGLE) = true
    | layoutEq _ = false

  (*%
     @formatter(BoundTypeVarID.Map.map) formatBtvenv
     @formatter(BoundTypeVarID.id) BoundTypeVarID.format_id
     @formatter(TypID.id) TypID.format_id
     @formatter(btvenv) formatBtvenv
     @formatter(list) SMLFormat.BasicFormatters.format_list
     @formatter(option) TermPrintUtils.formatPrependedOpt
     @formatter(SEnv.map) TermPrintUtils.formatSEnvMap
     @formatter(RecordLabel.Map.map) TermPrintUtils.formatRecordLabelMap
     @formatter(Symbol.longsymbol) Symbol.format_longsymbol
   *)
  datatype reifiedTy
    = (*%  @format(size) "internalTy"  *)
      INTERNALty of int 
    | (*%  @format(tid) tid *)
      BOUNDVARty of btvId
    | (*% @format "codeptr" *)
      CODEPTRty
    | (*% @format "exntag" *)
      EXNTAGty
    | (*% @format "int" *)
      INTty
    | (*% @format "int8" *)
      INT8ty
    | (*% @format "int16" *)
      INT16ty
    | (*% @format "int64" *)
      INT64ty
    | (*% @format "intInf" *)
      INTINFty
    | (*% @format "bool" *)
      BOOLty
    | (*% @format "word" *)
      WORDty
    | (*% @format "word8" *)
      WORD8ty
    | (*% @format "word16" *)
      WORD16ty
    | (*% @format "word64" *)
      WORD64ty
    | (*% @format "char" *)
      CHARty
    | (*% @format "string" *)
      STRINGty
    | (*% @format "real" *)
      REALty
    | (*% @format "real32" *)
      REAL32ty
    | (*% @format "unit" *)
      UNITty 
    | (*% @format "exn" *)
      EXNty
    | (*% @format "?X" *)
      TYVARty
    | (*% @format "?DummyTy" *)
      DUMMYty of {boxed : bool, size : word}
    | (*% @format "?ERRORty" *)
      ERRORty
    | (*% @format(arg args * resultTy)
            "{" args(arg)(",") + "}} -> " + resultTy
      *)
      FUNMty of reifiedTy list * reifiedTy
    | (*% @format(arg) arg + "list" *)
      LISTty of reifiedTy
    | (*% @format(arg) arg + "array" *)
      ARRAYty of reifiedTy
    | (*% @format(arg) arg + "vector" *)
      VECTORty of reifiedTy
    | (*% @format(arg) arg + "option" *)
      OPTIONty of reifiedTy
    | (*% @format(arg) arg + "ref" *)
      REFty of reifiedTy
    | (*% @format(arg) arg + "ptr" *)
      PTRty of reifiedTy
    | (*% 
        @format(ty tys) 
         !N0{ "{" 2[1 tys(ty)(":") ] 1"}" }
       *)
      RECORDty of reifiedTy RecordLabel.Map.map
    | (*% 
         @format({longsymbol, id, args:ty tys, layout, size}) 
           tys(ty)(",") + longsymbol "(" id ")" 
           + 2[1 layout]
       *)
      DATATYPEty of {longsymbol: Symbol.longsymbol, 
                     id:TypID.id, 
                     args: reifiedTy list,
                     layout: layout,
                     size : int
                    }
    | (*% 
         @format({longsymbol, id, args:ty tys, size}) 
           tys(ty)(",") + longsymbol "(" id ")" 
       *)
      OPAQUEty of {longsymbol: Symbol.longsymbol, 
                   id:TypID.id, 
                   size : int,
                   args: reifiedTy list
                  }
    | (*% 
         @format({boundenv:btv env, body:ty}) 
         "["env(btv)(",") "." + ty "]"
       *)
      POLYty of {boundenv: btvId BoundTypeVarID.Map.map, body: reifiedTy}
    | (*% 
         @format({longsymbol, id, args:ty tys, conSet, layout, size}) 
           tys(ty)(",") + longsymbol "(" id ")" 
           2[1 conSet]
           2[1 layout]
       *)
      CONSTRUCTty of {longsymbol: Symbol.longsymbol, 
                      id:TypID.id, 
                      args: reifiedTy list,
                      conSet : conSet,
                      layout: layout,
                      size : int
                     }

  withtype conSet = 
      (*% 
         @format(tyOpt tys) 
         !N0{ "("  2[1 tys(tyOpt)() ] 1 ")" }
         @format:tyOpt(ty opt)
           opt(ty)(+"of"+)
       *)
       (reifiedTy option) SEnv.map

  fun reifiedTyToString reifiedTy =
      let
        val _ = Bug.printError "ReifiedTy reifiedTyToString\n";
        val f = format_reifiedTy reifiedTy
        val _ = Bug.printError "ReifiedTy after format\n";
        val s = SMLFormat.prettyPrint nil f
        val _ = Bug.printError "ReifiedTy after reifiedTyToString\n";
      in
         s
      end

  fun reifiedTyEq (ty1, ty2) =
      let
        val emptyEquivEnv = BoundTypeVarID.Map.empty
        fun extendEquivEnv (btvenv1, btvenv2) equivEnv =
            ListPair.foldlEq 
              (fn (i, j, map) => BoundTypeVarID.Map.insert(map, i, j))
              equivEnv
              (BoundTypeVarID.Map.listKeys btvenv1,
               BoundTypeVarID.Map.listKeys btvenv2)
            handle ListPair.UnequalLengths => equivEnv
        fun btvIdEq equivEnv (id1, id2) = 
            BoundTypeVarID.eq(id1, id2) 
            orelse
            case BoundTypeVarID.Map.find(equivEnv, id1) of
              SOME id3 => BoundTypeVarID.eq(id3, id2) 
            | NONE => 
              (case BoundTypeVarID.Map.find(equivEnv, id2) of
                 SOME id4 => BoundTypeVarID.eq(id1, id4) 
               | NONE => false)
        fun eqList equivEnv (nil, nil) = true
          | eqList equivEnv (h::_, nil) = false
          | eqList equivEnv (nil, h::_) = false
          | eqList equivEnv (h1::tl1, h2::tl2) =
            eq equivEnv (h1, h2) andalso eqList equivEnv (tl1, tl2)
        and eq equivEnv (ty1,ty2) =
            case (ty1,ty2) of
              (ARRAYty ty1, ARRAYty ty2) => eq equivEnv (ty1, ty2)
            | (BOOLty, BOOLty) => true
            | (BOUNDVARty id1, BOUNDVARty id2) => btvIdEq equivEnv (id1, id2)
            | (CHARty, CHARty) => true
            | (CODEPTRty, CODEPTRty) => true
            | (CONSTRUCTty {id=id1, args=args1,...}, CONSTRUCTty {id=id2, args=args2,...})
              => TypID.eq (id1, id2) andalso eqList equivEnv (args1, args2)
            | (DATATYPEty  {id=id1, args=args1,...}, DATATYPEty {id=id2, args=args2,...}) 
              => TypID.eq (id1, id2) andalso eqList equivEnv (args1, args2)
            | (DUMMYty {boxed=b1, size=s1}, DUMMYty {boxed=b2, size=s2}) =>
              b1 = b2 andalso s1 = s2
            | (ERRORty, ERRORty)  => true
            | (EXNTAGty, EXNTAGty) => true
            | (EXNty, EXNty) => true
            | (FUNMty ([arg1], ty1), FUNMty ([arg2], ty2)) => 
              eq equivEnv (arg1, arg2) andalso eq equivEnv (ty1, ty2)
            | (INTty, INTty) => true
            | (INT8ty, INT8ty) => true
            | (INT16ty, INT16ty) => true
            | (INT64ty, INT64ty) => true
            | (INTERNALty int1, INTERNALty int2) =>  int1 = int2
            | (INTINFty, INTINFty) => true
            | (LISTty  ty1, LISTty  ty2) => eq equivEnv (ty1, ty2)
            | (OPAQUEty {id=id1, args=args1,...}, OPAQUEty {id=id2, args=args2,...})
              => TypID.eq (id1, id2) andalso eqList equivEnv (args1, args2)
            | (OPTIONty ty1, OPTIONty ty2) => eq equivEnv (ty1, ty2)
            | (POLYty {boundenv=benv1, body=ty1}, POLYty {boundenv=benv2, body=ty2})
              => eq (extendEquivEnv (benv1, benv2) equivEnv) (ty1, ty2)
            | (PTRty ty1, PTRty ty2) => eq equivEnv (ty1, ty2)
            | (REAL32ty, REAL32ty) => true
            | (REALty, REALty) => true
            | (RECORDty map1, RECORDty map2) => RecordLabel.Map.eq (eq equivEnv) (map1, map2)
            | (REFty ty1, REFty ty2)  => eq equivEnv (ty1, ty2)
            | (STRINGty, STRINGty) => true
            | (TYVARty, TYVARty) => true
            | (UNITty , UNITty) => true
            | (VECTORty ty1, VECTORty ty2)  => eq equivEnv (ty1, ty2)
            | (WORDty, WORDty) => true
            | (WORD8ty, WORD8ty) => true
            | (WORD16ty, WORD16ty) => true
            | (WORD64ty, WORD64ty) => true
            | _ => false
      in
        eq emptyEquivEnv (ty1, ty2)
      end

  val emptyConSet = SEnv.empty : conSet
  fun conSetToString conSet =
      SMLFormat.prettyPrint nil (format_conSet conSet)
  fun conSetEq (x:conSet, y:conSet) = 
      let
        fun reifiedTyOptEq (NONE, NONE) = true
          | reifiedTyOptEq (SOME a, SOME b) = reifiedTyEq (a,b)
          | reifiedTyOptEq _ = false
      in
        SEnv.isEmpty
          (SEnv.mergeWith 
             (fn (SOME a, SOME b) => if reifiedTyOptEq (a, b) then NONE else SOME (SOME a,SOME b)
               | (SOME a, NONE) => SOME (SOME a, NONE)
               | (NONE, SOME b) => SOME (NONE, SOME b)
               | (NONE, NONE) => NONE
             )
             (x,y)
          )
      end

  fun mkConSet stringReifiedTyOptlist =
      foldl
        (fn ((string, tyOpt), map) =>
            SEnv.insert
              (map, string, tyOpt))
        emptyConSet
        stringReifiedTyOptlist

  (*%
     @formatter(TypID.Map.map) TermPrintUtils.formatTypIDMap
  *)
  type conSetEnv = 
      (*% 
         @format(conset map) 
         !N0{
           "{"
              2[1 map(conset)("=>") ]
            1
           "}"
         }
       *)
       conSet TypID.Map.map
  val emptyConSetEnv = TypID.Map.empty : conSetEnv
  fun conSetEnvToString conSetEnv =
      SMLFormat.prettyPrint nil (format_conSetEnv conSetEnv)
  fun conSetEnvEq (conSetMap1, conSetMap2) =
      TypID.Map.eq conSetEq (conSetMap1, conSetMap2)
  fun lookUpConSet (conSetEnv, id) = 
      case TypID.Map.find(conSetEnv, id) of
        NONE => raise ConSetNotFound
      | SOME conSet => conSet
  fun insertConSet (conSetEnv, id, conSet) =
      TypID.Map.insert(conSetEnv, id, conSet)

  val globalConSetEnv = ref TypID.Map.empty : conSetEnv ref
  fun resetGlobalConSetEnv () = globalConSetEnv := TypID.Map.empty
  fun getGlobalConSetEnv () = !globalConSetEnv

  fun findConSet id = TypID.Map.find (!globalConSetEnv, id)

  fun setConSet (id, conSet) =
       globalConSetEnv:= insertConSet (!globalConSetEnv, id, conSet)

  (*%
    @formatter(NonEmptyTypIDMap) TermPrintUtils.formatNonEmptyTypIDMap
   *)
  type tyRep = 
       (*%
          @format({conSetEnv, reifiedTy})
           "let"
             2[+1 conSetEnv:NonEmptyTypIDMap()(conSetEnv)]
            +1
            "in"
             2[+1 reifiedTy]
            +1 
            "end"
        *)
       {conSetEnv: conSetEnv, reifiedTy : reifiedTy}

  fun tyRepToString tyRep =
      SMLFormat.prettyPrint nil (format_tyRep tyRep)
  fun tyRepEq ({conSetEnv=env1, reifiedTy=ty1},{conSetEnv=env2, reifiedTy=ty2}) =
      conSetEnvEq (env1, env2) andalso reifiedTyEq (ty1, ty2)

  fun applySubst subst ty =
      let
        val apply = applySubst subst
      in
        case ty of
          BOUNDVARty id => 
          (case BoundTypeVarID.Map.find(subst, id) of
             NONE => ty | SOME newTy => newTy)
        | LISTty reifiedTy => LISTty (apply reifiedTy)
        | ARRAYty reifiedTy => LISTty (apply reifiedTy)
        | VECTORty reifiedTy => LISTty (apply reifiedTy)
        | OPTIONty reifiedTy => LISTty (apply reifiedTy)
        | REFty reifiedTy => LISTty (apply reifiedTy)
        | RECORDty reifiedTyMap =>
          RECORDty (RecordLabel.Map.map apply reifiedTyMap)
        | DATATYPEty {longsymbol, id, args, layout, size} =>
          DATATYPEty {longsymbol=longsymbol, id=id, args=map apply args, layout=layout, size=size}
        | POLYty {boundenv, body} =>
          POLYty {boundenv=boundenv, body = apply body}
        | _ => ty
      end

  fun instantiate (ty as (POLYty {boundenv, body}), args) =
      let
        val idList = BoundTypeVarID.Map.listKeys boundenv
        val idTyPairs = 
            if length args = length idList then ListPair.zip (idList, args)
            else 
              (print "instantiate arity\n";
               print (reifiedTyToString ty);
               print "\n";
               map (fn x => print (reifiedTyToString x ^ "\n")) args;
              raise InstantiateFail
              )
        val subst = foldr
                    (fn ((id,ty), newBoundEnv) =>
                        BoundTypeVarID.Map.insert(newBoundEnv, id, ty))
                    BoundTypeVarID.Map.empty
                    idTyPairs
      in
        applySubst subst body
      end
    | instantiate (ty, nil) =  ty
    | instantiate (ty, args) = 
      (print "instantiate type case\n";
       print (reifiedTyToString ty);
       print "\n";
       map (fn x => print (reifiedTyToString x ^ "\n")) args;
       raise InstantiateFail
      )

  fun getConSet {reifiedTy = DATATYPEty {longsymbol, id, args, layout, size}, conSetEnv} =
      let
        val conSet = lookUpConSet (conSetEnv, id)
      in
        SEnv.map
        (fn NONE => NONE
          | SOME ty => SOME (instantiate (ty, args)))
        conSet
      end
    | getConSet _ = raise GetConSetFail

  fun getConstructTy (tyRep as {reifiedTy = DATATYPEty {longsymbol, id, args, layout, size}, conSetEnv}) = 
      let
        val conSet = getConSet tyRep
      in
        {conSetEnv = conSetEnv,
         reifiedTy = CONSTRUCTty {longsymbol = longsymbol, id = id, args = args, conSet = conSet, layout=layout, size=size}}
      end
    | getConstructTy tyRep = tyRep

  fun stringReifiedTyListToRecordTy stringReifiedTyList =
      RECORDty
        (foldr
           (fn ((string, reifiedTy), labelMap) => 
               RecordLabel.Map.insert(labelMap, RecordLabel.fromString string, reifiedTy))
           RecordLabel.Map.empty
           stringReifiedTyList)

  fun stringReifiedTyOptionListToConSet stringReifiedTyOptionList =
      foldr
      (fn ((string, reifiedTyOpt), conSet) =>
          SEnv.insert(conSet, string, reifiedTyOpt))
      SEnv.empty
      stringReifiedTyOptionList

  fun typIdConSetListToConSetEnv typIdConSetList =
      foldr
      (fn ((typId, conSet), conSetEnv) =>
          TypID.Map.insert(conSetEnv, typId, conSet))
      TypID.Map.empty
      typIdConSetList

  fun btvIdBtvIdListToBoundenv btvIdBtvIdList =
      foldr
      (fn ((btvid1, btvid2), boundenv) =>
          BoundTypeVarID.Map.insert(boundenv, btvid1, btvid2))
      BoundTypeVarID.Map.empty 
      btvIdBtvIdList
      
  fun boundenvReifiedTyToPolyTy boundenv reifiedTy =
      POLYty {boundenv = boundenv, body = reifiedTy}

  fun makeDummyTy boxed size =
      DUMMYty {boxed = boxed, size = size}
      
  fun makeFUNMty reifiedTyList reifiedTy =
      FUNMty (reifiedTyList, reifiedTy)
      
  fun stringIntListToTagMap stringIntList =
      let
        val tagMap = 
            foldl (fn ((s, i), tagMap) => SEnv.insert(tagMap, s, i))
                  SEnv.empty
                  stringIntList
      in
        tagMap
      end

  fun stringIntListToTagMap stringIntList =
      foldl (fn ((s, i), tagMap) => SEnv.insert(tagMap, s, i))
            SEnv.empty
            stringIntList
      
  fun tagMapToTagMapRecord tagMap = {tagMap = tagMap}

  fun tagMapStringToTagMapNullNameRecord tagMap nullName =
      {tagMap = tagMap, nullName=nullName}
      
  fun stringToFalseNameRecord string = {falseName = string}

  fun boolToWrapRecord bool = {wrap = bool}

  fun TyRep conSetEnv reifiedTy =
      {conSetEnv = conSetEnv, reifiedTy = reifiedTy}

  fun longsymbolIdArgsLayoutListToDatatypeTy longsymbol id args layout size =
      DATATYPEty {longsymbol=longsymbol, id = id, args = args, layout=layout, size = size}

  fun longsymbolIdArgsToOpaqueTy longsymbol id args size =
      OPAQUEty {longsymbol=longsymbol, id = id, args = args, size = size}

  fun makePos filename line col = 
      Loc.makePos {fileName=filename, line=line, col=col}
end

