
open InstructionParserTypes;

val lastOpcode = ref 0;

%%
%name		Instruction
%term           LBRACE | RBRACE |
                EQ | COLON | SEMICOLON | COMMA | VBAR |
                STRUCTURE | STRUCT | END |
                DATATYPE | OF |
	        STRING of string |
                UINT8 |
	        SINT8 |
	        UINT16 |
	        SINT16 |
	        UINT24 |
	        SINT24 |
	        UINT32 |
	        SINT32 |
	        REAL32 |
	        REAL64 |
	        LIST |
                EOF

%nonterm        StructureDefinition of
                (string * string * (instructionDefinition list)) |
                DatatypeDefinition of (string * (instructionDefinition list)) |
                Instructions of instructionDefinition list |
                Instruction of instructionDefinition |
                Opname of string |
                Operands of (string * operandType) list |
                Operand of (string * operandType) |
                OperandType of operandType

%pos            {fileName:string, line:int, col:int}
%eop		EOF
%noshift	EOF

%verbose

%%
StructureDefinition:
    STRUCTURE STRING EQ STRUCT DatatypeDefinition END
        (
          (STRING, #1 DatatypeDefinition, #2 DatatypeDefinition)
          before lastOpcode := 0
        )

DatatypeDefinition:
    DATATYPE STRING EQ Instructions  
        (STRING, Instructions)

Instructions:
    Instructions VBAR Instruction  (Instructions@[Instruction])
  | Instruction ([Instruction])

Instruction:
    Opname OF LBRACE Operands RBRACE
                ({
                   name = Opname,
                   opcode = !lastOpcode before (lastOpcode := !lastOpcode + 1),
                   operands = Operands,
                   location = (Opnameleft, RBRACEright)
                 })
  | Opname
                ({
                   name = Opname,
                   opcode = !lastOpcode before (lastOpcode := !lastOpcode + 1),
                   operands = [],
                   location = (Opnameleft, Opnameright)
                 })


Opname:
    STRING    (STRING)

Operands:
    Operands COMMA Operand    (Operands @ [Operand])
  | Operand                   ([Operand])

Operand:
    STRING COLON OperandType  (STRING, OperandType)

OperandType:
    UINT8	(UInt8)
  | SINT8	(SInt8)
  | UINT16	(UInt16)
  | SINT16	(SInt16)
  | UINT24	(UInt24)
  | SINT24	(SInt24)
  | UINT32	(UInt32)
  | SINT32	(SInt32)
  | REAL32	(Real32)
  | REAL64	(Real64)
  | OperandType LIST	(List(OperandType))

